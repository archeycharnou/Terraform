
#####################################
# Elastic IP
#####################################

# Allocate Elastic IP for NAT Gateway
resource "aws_eip" "eip" {
  domain = "vpc"
}


#####################################
# VPC
#####################################

## VPC resource
resource "aws_vpc" "main" {
  cidr_block           = var.vpc_cidr
  enable_dns_support   = true
  enable_dns_hostnames = true

  tags = {
    Name = "main-vpc"
  }
}

#####################################
# GATEWAYS
#####################################

## IGW resource
resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name = "main-igw"
  }
}

# NAT Gateway Resource
resource "aws_nat_gateway" "nat" {
  allocation_id = aws_eip.eip.id
  # NAT Gateway must be in a public subnet
  subnet_id = aws_subnet.public[0].id
  # Makes sure that the internet gateway is setup first.
  depends_on = [ aws_internet_gateway.igw ]
  tags = {
    Name = "nat-gateway"
  }
}


#####################################
# SUBNETS
#####################################

## Resource for 2 Pub Subnets
resource "aws_subnet" "public" {
  count                   = 2
  vpc_id                  = aws_vpc.main.id
  cidr_block              = var.public_subnet_cidrs[count.index]
  availability_zone       = var.availability_zones[count.index]
  map_public_ip_on_launch = true

  tags = {
    Name = "public-subnet-${count.index + 1}"
  }
}

## Resource for 2 Private Subnets
resource "aws_subnet" "private" {
  count             = 2
  vpc_id            = aws_vpc.main.id
  cidr_block        = var.private_subnet_cidrs[count.index]
  availability_zone = var.availability_zones[count.index]

  tags = {
    Name = "private-subnet-${count.index + 1}"
  }
}


#####################################
# ROUTE TABLES
#####################################

## Public Route Table Resource
resource "aws_route_table" "public_rt" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }

  tags = {
    Name = "public-route-table"
  }
}

# Private Route Table Resource
resource "aws_route_table" "private_rt" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block     = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.nat.id
  }

  tags = {
    Name = "private-route-table"
  }
}

## Associate Pub Routing table with Pub Subnets
resource "aws_route_table_association" "public_association" {
  count          = length(var.public_subnet_cidrs)
  subnet_id      = aws_subnet.public[count.index].id
  route_table_id = aws_route_table.public_rt.id
}

# Associate Private Route Table with Private Subnets
resource "aws_route_table_association" "private_association" {
  count          = length(var.private_subnet_cidrs)
  subnet_id      = aws_subnet.private[count.index].id
  route_table_id = aws_route_table.private_rt.id
}


